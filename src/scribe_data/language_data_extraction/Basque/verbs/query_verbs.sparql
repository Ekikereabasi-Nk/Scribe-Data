# tool: scribe-data
# All Basque (Q8752) verbs.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?future
  ?gerund
  ?imperfective
  ?nominalized
  ?participle
  ?sense

WHERE {
  # Infinitive form
  ?lexeme dct:language wd:Q8752 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .

  # Future Tense
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureForm .
    ?futureForm ontolex:representation ?future ;
    wikibase:grammaticalFeature wd:Q501405 .  # Future tense
    FILTER(LANG(?future) = "eu") .
  } .

  # Gerund
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?gerundForm .
    ?gerundForm ontolex:representation ?gerund ;
    wikibase:grammaticalFeature wd:Q1923028 .  # Gerund
    FILTER(LANG(?gerund) = "eu") .
  } .

  # Imperfective Verb
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperfectiveForm .
    ?imperfectiveForm ontolex:representation ?imperfective ;
    wikibase:grammaticalFeature wd:Q54556033 .  # Imperfective
    FILTER(LANG(?imperfective) = "eu") .
  } .

  # Nominalized Verb
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominalizedForm .
    ?nominalizedForm ontolex:representation ?nominalized ;
    wikibase:grammaticalFeature wd:Q74674960 .  # Nominalization
    FILTER(LANG(?nominalized) = "eu") .
  } .

  # Participle
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?participleForm .
    ?participleForm ontolex:representation ?participle ;
    wikibase:grammaticalFeature wd:Q814722 .  # Participle
    FILTER(LANG(?participle) = "eu") .
  } .

  # Senses (meanings)
  OPTIONAL {
    ?lexeme ontolex:sense ?senseURI .
    ?senseURI skos:definition ?sense .
    FILTER(LANG(?sense) = "eu") .
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],eu".
  }
}
