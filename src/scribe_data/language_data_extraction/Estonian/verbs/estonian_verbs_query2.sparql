# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Grammatical Forms (Present participle, Past participle, Transgressive) 26118 results in 7342 ms



SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?presentParticiple
  ?pastParticiple
  ?transgressive
WHERE {
  ?lexeme dct:language wd:Q9072 ;
          wikibase:lexicalCategory wd:Q24905 ;
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentParticipleForm .
    ?presentParticipleForm ontolex:representation ?presentParticiple ;
    wikibase:grammaticalFeature wd:Q10345583 .
    FILTER(LANG(?presentParticiple) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
    wikibase:grammaticalFeature wd:Q1230649 .
    FILTER(LANG(?pastParticiple) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?transgressiveForm .
    ?transgressiveForm ontolex:representation ?transgressive ;
    wikibase:grammaticalFeature wd:Q904896 .
    FILTER(LANG(?transgressive) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
