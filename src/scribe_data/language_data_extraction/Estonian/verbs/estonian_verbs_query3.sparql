# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Verbs with Simple Past, Conditional, Imperative, and Jussive forms 368405 results in 39520 ms


SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?simplePast
  ?conditional
  ?imperative
  ?jussive
WHERE {
  ?lexeme dct:language wd:Q9072 ;
          wikibase:lexicalCategory wd:Q24905 ;
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastForm .
    ?simplePastForm ontolex:representation ?simplePast ;
    wikibase:grammaticalFeature wd:Q1392475 .
    FILTER(LANG(?simplePast) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conditionalForm .
    ?conditionalForm ontolex:representation ?conditional ;
    wikibase:grammaticalFeature wd:Q625581 .
    FILTER(LANG(?conditional) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
    wikibase:grammaticalFeature wd:Q22716 .
    FILTER(LANG(?imperative) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?jussiveForm .
    ?jussiveForm ontolex:representation ?jussive ;
    wikibase:grammaticalFeature wd:Q462367 .
    FILTER(LANG(?jussive) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
