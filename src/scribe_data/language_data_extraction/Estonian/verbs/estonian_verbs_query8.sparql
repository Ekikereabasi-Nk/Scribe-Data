# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Verbs with Third Person forms 38072 results in 6488 ms

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?thirdPerson
WHERE {
  ?lexeme dct:language wd:Q9072 ;  # Estonian language
          wikibase:lexicalCategory wd:Q24905 ;  # Verb category
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonForm .
    ?thirdPersonForm ontolex:representation ?thirdPerson ;
    wikibase:grammaticalFeature wd:Q51929074 .  # Third person
    FILTER(LANG(?thirdPerson) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
